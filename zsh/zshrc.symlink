#!/bin/zsh

PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2>$HOME/tmp/startlog.$$
    setopt xtrace prompt_subst
fi

# Shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# All of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files updated_at

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

fpath=(~/.zsh $fpath)

# The next line updates PATH for the Google Cloud SDK.
if [ -f "${HOME}/google-cloud-sdk/path.zsh.inc" ]; then
  source "${HOME}/google-cloud-sdk/path.zsh.inc"
fi

# Editor
# Convenience aliases for editing configs
if type nvim > /dev/null 2>&1; then
  export EDITOR=nvim
  alias vim='nvim'
fi
export USE_EDITOR=$EDITOR
export VISUAL=$EDITOR
alias ev='vim ~/.vimrc'
alias et='vim ~/.tmux.conf'
alias ez='vim ~/.zshrc'

# OpenSSL
export CPPFLAGS=-I/usr/local/opt/openssl/include
export LDFLAGS=-L/usr/local/opt/openssl/lib
export PKG_CONFIG_PATH=/usr/local/opt/openssl/lib/pkgconfig

# Path
PATH="/usr/local/sbin:$PATH"
PATH=/usr/local/share/python:$PATH
PATH=/usr/local/opt/openssl/bin:$PATH
PATH="$HOME/.tfenv/bin:$PATH"
PATH="$HOME/.dotfiles/bin:$PATH"
# Setting PATH for Python 3.6
# The original version is saved in .zprofile.pysave
PATH="/Library/Frameworks/Python.framework/Versions/3.6/bin:${PATH}"
PATH="$HOME/.cargo/bin:$PATH"
PATH="$HOME/.tgenv/bin:$PATH"
export PATH

# Source private commands
# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [ -f ~/.localrc ]; then
  source ~/.localrc
fi

# Base16 Shell
BASE16_SCHEME="chalk"
BASE16_SHELL="$HOME/.extra/.zsh/base16-shell/base16-$BASE16_SCHEME.dark.sh"
[[ -s $BASE16_SHELL ]] && source $BASE16_SHELL

# Profiling END
# Entirety of my startup file... then
if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
source ~/.autoenv/activate.sh

eval "$(direnv hook zsh)"
